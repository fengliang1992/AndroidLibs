import groovy.json.JsonBuilder

apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compileOnly 'com.android.support:recyclerview-v7:26.1.0'
}

apply plugin: 'maven'

def currentVersion = '2.0.22'
def versionDes = 'Fix bugs'
def websiteUrl = 'https://github.com/leochuan/ViewPagerLayoutManager'
def date = new Date()
def projectName = 'viewpager-layout-manager'

task createPom << {
    pom {
        project {
            groupId 'rouchuan.viewpagerlayoutmanager'
            artifactId 'viewpagerlayoutmanager'
            version currentVersion
            packaging 'aar'
            name projectName
            url websiteUrl
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'leochuan'
                    name 'Shen Ruo Chuan'
                    email 'ruochuan.leo@gmail.com'
                }
            }
            scm {
                connection "${websiteUrl}.git"
                developerConnection "${websiteUrl}.git"
                url websiteUrl
            }
        }
    }.writeTo("build/viewpagerlayoutmanager-${currentVersion}.pom")

    def builder = new JsonBuilder()
    def root = builder {
        'package' {
            name projectName
            repo 'maven'
            subject 'leochuan'
            desc 'ViewPager like LayoutManager which supports some awesome animations and infinite scroll.'
            website_url websiteUrl
            issue_tracker_url "$websiteUrl/issues"
            vcs_url "${websiteUrl}.git"
            github_use_tag_release_notes true
            github_release_notes_file 'RELEASE_NOTE.md'
            labels "android", "layoutManager"
            public_download_numbers false
            public_stats false
        }
        'version' {
            name currentVersion
            desc versionDes
            released date.format('yyyy-MM-dd')
            vcs_tag "v${currentVersion}"
            gpgSign false
        }
        publish true
    }
    root.files = []
    root.files.add([
            'includePattern': 'viewpagerlayoutmanager/build/libs/(.*\\.jar)',
            'uploadPattern' : "rouchuan/viewpagerlayoutmanager/viewpagerlayoutmanager/$currentVersion/\$1",
            'matrixParams'  : ['override': 1]
    ])
    root.files.add([
            'includePattern': 'viewpagerlayoutmanager/build/(.*\\.pom)',
            'uploadPattern' : "rouchuan/viewpagerlayoutmanager/viewpagerlayoutmanager/$currentVersion/\$1",
            'matrixParams'  : ['override': 1]
    ])
    root.files.add([
            'includePattern': 'viewpagerlayoutmanager/build/outputs/aar/(.*release\\.aar)',
            'uploadPattern' : "rouchuan/viewpagerlayoutmanager/viewpagerlayoutmanager/$currentVersion/viewpagerlayoutmanager-${currentVersion}.aar",
            'matrixParams'  : ['override': 1]
    ])

    def jsonFile = new File("descriptor.json")
    jsonFile.write(builder.toPrettyString())
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = currentVersion
}

artifacts {
    archives sourcesJar
}

build.dependsOn('createPom')